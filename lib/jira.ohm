/*
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */

/*
 * Copyright 2019, Joyent, Inc.
 */

/*
 * jiramark: JIRA markup grammar
 */
JIRA {
  Document = blocks tblock?

  blocks = (tblock (space* nl)+)*

  tblock
   = block
   | (words (&paraEnd | nl))+ -- paragraph

  block
   = heading space+ words -- header
   | readUntil<"noformat"> -- noformat
   | readUntil<"code"> -- code
   | namedBlock<"panel"> -- panel
   | namedBlock<"quote"> -- quote
   | namedBlock<"color"> -- color
   | bq space+ words -- blockquote
   | (bullet (space* ~bullet (block | words) nl)+)+ -- list

  paraEnd
   = nl? heading -- headStart
   | nl? blockStart -- blockStart
   | nl? bq -- bqStart
   | nl? bullet -- listStart
   | nl space* nl -- blankStart

  readUntil<nm> = space* "{" nm options? "}" upToThreeNl (~("{" nm) any)* "{" nm "}"
  namedBlock<nm> = space* "{" nm options? "}" nl* (~("{" nm) tblock nl*)* (~("{" nm) tblock)? "{" nm "}"
  upToThreeNl = nl? nl? nl?

  options = ":" (~"}" (ident | space))*

  heading = "h1." | "h2." | "h3." | "h4." | "h5." | "h6."
  bq = "bq."

  bullet = space* point (bullet | space)
  point (a bullet point) = "#" | "-" | "*"

  words
   = formatted+ word* -- type1
   | word+ -- type2

  word
   = ~punct ~bareurl ident+ -- basic
   | simpleWord

  simpleWord
   = "[" simpleWordUntil<"|"> "|" space* uri space* "]" -- link1
   | "[" ~"~" uri "]" -- link2
   | "[~" (~"]" (ident | space))+ "]" -- user
   | "{{" simpleWordUntil<"}}"> "}}" -- monospace
   | (space | ~formchar punct) formatted+ -- formatted
   | bareurl
   | ident
   | space

  formatted
   = "??" ~space simpleWordUntil<"??"> "??" -- citation
   | "*" ~space simpleWordUntil<"*"> "*" -- strong
   | "_" ~space simpleWordUntil<"_"> "_" -- emphasis
   | "~" ~space simpleWordUntil<"~"> "~" -- subscript
   | "^" ~space simpleWordUntil<"^"> "^" -- superscript
   | "-" ~space simpleWordUntil<"-"> "-" -- deleted
   | "+" ~space simpleWordUntil<"+"> "+" -- inserted

  simpleWordUntil<stop>
   = ~stop formatted (~stop simpleWord)* -- multiple
   | (~stop simpleWord)+ -- simple

  bareurl = "http" "s"? "://" (~urlend urichar)+
  urlend = urlexcl* ~urichar
  urlexcl = "." | ")" | "!" | "," | "'"

  space := " " | "\t"

  nl (a newline) = "\r\n" | "\n" | "\r"

  blockStart = "{quote" | "{panel" | "{color" | "{noformat" | "{code"

  formchar (a format character) = "*" | "_" | "~" | "-" | "+" | "^" | "??"
  punct = ~alnum ~space ~nl "\u0021".."\u007E"
  escapes = formchar | "[" | "]" | "{" | "}" | "\\"

  ident = ~blockStart (chars | punct)

  chars = (entchar | escchar | regchar)+
  entchar (an HTML entity) = "&" "#"? alnum+ ";"
  escchar (an escape sequence) = "\\" escapes
  regchar (a regular character) = ~(space | nl | punct) any

  uri (a URI) = urichar+
  urichar = alnum | "!" | "\u0023".."\u002F" | ":" | ";" | "=" | "?" | "@" | "_" | "~"
}
